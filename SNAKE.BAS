
1  REM A BASIC SNAKE GAME FOR THE APPLE II.
2  REM SX AND SY HOLD COORDINATES FOR SNAKE SEGMENTS
3  REM THEY ARE STRUCTURED AS A CIRCULAR BUFFER INDEXED BY HI 
4  REM AND TI WHICH POINT AT THE FIRST AND LAST SEGMENTS
5  REM RESPECTIVELY. MA IS A MAP OF THE SCREEN WHICH ALLOWS
6  REM QUICK TESTING FOR SNAKE CRASHING INTO ITSELF. AN
7  REM ARGUABLY BETTER APPROACH WOULD BE TO PEEK INTO SCREEN
8  REM MEMORY INSTEAD OF THE MA ARRAY.

10  DIM SX(920): DIM SY(920): REM X AND Y COORDINATES OF SNAKE SEGMENTS
20  DIM MA(920): REM MAP OF THE SCREEN
30  HOME

50  REM GAME (RE)START
60  PRINT ""
61  PRINT "DRIVE YOUR SNAKE WITH WASD KEYS."
62  PRINT "EAT FOOD. DON'T CRASH INTO YOUR SNAKE"
63  PRINT "OR THE WALLS."
64  PRINT ""
65  PRINT "PRESS Q TO QUIT OR ANY OTHER KEY TO START"
66  PRINT ""
70  GET X$
80  IF X$ = "Q" GOTO 3000
90  PRINT "INITIALIZING..."
100  FOR I=0 to 919: MA(I) = 64: HTAB 20: NEXT
110  LET HI = 0: LET TI = 0: REM INDEX OF HEAD AND TAIL 
120  LET HX = 19: LET HY = 11: REM COORDINATES OF HEAD
130  LET SC = 1: REM SCORE
140  HOME
150  HTAB HX + 1: VTAB HY + 1: PRINT "S";
160  LET MA(HY * 40 + HX) = 83
170  HTAB 4: VTAB 24: PRINT "-------------" ;
180  HTAB 19: VTAB 24: PRINT SC;
190  HTAB 24: VTAB 24: PRINT "-------------" ;

200  REM GENERATE FOOD
220  LET FX = INT(RND(1) * 40)
230  LET FY = INT(RND(1) * 23)
240  IF FX=HX AND FY=HY GOTO 200
250  HTAB FX + 1: VTAB FY + 1: PRINT "F";
260  LET MA(FY * 40 + FX) = 70

300  REM READ KEYPRESS. TRANSLATE WASD INTO DX AND DY
310  LET K = PEEK (49152) - 128
320  REM CHECK FOR 'W' FOR UP
325  IF K = 87 THEN LET DX =  0: LET DY = - 1: GOTO 400
330  REM CHECK FOR 'A' FOR LEFT
335  IF K = 65 THEN LET DX = - 1: LET DY =  0: GOTO 400
340  REM CHECK FOR 'S' FOR DOWN
345  IF K = 83 THEN LET DX = 0: LET DY = 1: GOTO 400
350  REM CHECK FOR 'D' FOR RIGHT
355  IF K = 68 THEN LET DX = 1: LET DY = 0: GOTO 400
360  REM CHECK FOR 'Q' TO QUIT OR GET ANOTHER KEY
365  IF K <  > 81 GOTO 300
370  GET X$ : REM CLEAR LAST KEY BEFORE QUITTING
380  GOTO 3000

400  REM RECORD OLD HEAD COORDINATE AND CALC NEW ONE
401  REM WE'LL UPDATE HEAD INDEX LATER
410  LET SX(HI) = HX
420  LET SY(HI) = HY
430  LET HX = HX + DX
440  LET HY = HY + DY

500  REM CHECK FOR COLLISIONS
510  IF HX < 0 GOTO 2000
520  IF HY < 0 GOTO 2000
530  IF HX > 39 GOTO 2000
540  IF HY > 22 GOTO 2000
550  REM CHECK FOR SNAKE COLLISION
560  IF MA(HY * 40 + HX) = 83 GOTO 2000

600  REM DRAW NEW HEAD AND CHECK FOR WIN
610  HTAB HX + 1: VTAB HY + 1: PRINT "S";
620  LET MA(HY * 40 + HX) = 83
630  LET HI = HI + 1 : REM NEW HEAD INDEX
640  IF HI >= 920 THEN HI = 0
650  IF HI = TI THEN GOTO 1000: REM WINNER

700  REM CHECK FOR FOOD
701  REM IF FOUND, DON'T CHOP TAIL BUT
702  REM GO GROW MORE FOOD
710  IF HX <> FX OR HY <> FY GOTO 800
720  LET SC = SC + 1
730  HTAB 19: VTAB 24: PRINT SC;
740  GOTO 200

800  REM DIDN'T FIND FOOD
801  REM CHOP TAIL AND MOVE AGAIN
810  IF SX(TI) = FX AND SY(TI) = FY GOTO 840
820  HTAB SX(TI) + 1: VTAB SY(TI) + 1: PRINT " ";
830  LET MA(SY(TI) * 40 + SX(TI)) = 64
840  LET TI = TI + 1: REM NEW TAIL INDEX
850  IF TI >= 920 THEN TI = 0
860  GOTO 300

1000  REM WINNER
1010  HTAB 1: VTAB 1: PRINT "CONGRATULATIONS, YOU WON!"
1020  GET X$: REM CLEAR THE LAST KEY PRESSED
1030  GOTO 50

2000  REM LOSER
2010  HTAB 1: VTAB 1: PRINT "OOF. TRY AGAIN."
2020  GET X$: REM CLEAR THE LAST KEY PRESSED
2030  GOTO 50

3000  REM QUITTING
3010  PRINT "GOOD BYE"
3020  END

